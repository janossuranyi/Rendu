
#include "common_pbr.glsl"
#include "utils.glsl"

layout(local_size_x=8, local_size_y=8, local_size_z=1) in;

layout(set = 2, binding = 0) uniform textureCube texture0; ///< Input cubemap to process.

layout(set = 0, binding = 0) uniform UniformBlock {
	float mipmapRoughness; ///< The roughness to use for the convolution lobe.
	float clampMax; ///< Clamp input HDR values to avoid artefacts.
	int samplesCount; ///< Number of samples to take, higher count helps avoiding artifacts in bright areas.
};

layout(set = 2, binding = 1, rgba16f) uniform writeonly imageCube outCube; ///< Output cubemap level to write to.

/** Perform convolution with the BRDF specular lobe, evaluated in a given direction. 
\param r the reflection direction
\param roughness the roughness to evalute the BRDF at
\return the result of the convolution
*/
vec3 convo(vec3 r, float roughness){
	// Fix view and normal as in the Unreal white paper.
	vec3 v = r;
	vec3 n = r;

	// Compute local frame.
	vec3 temp = abs(n.z) < 0.999 ? vec3(0.0,0.0,1.0) : vec3(1.0,0.0,0.0);
	vec3 tangent = normalize(cross(temp, n));
	vec3 binormal = cross(n, tangent);

	float alpha = max(0.000, roughness*roughness);

	vec3 sum = vec3(0.0);
	float denom = 0.0;
	for(int i = 0; i < samplesCount; ++i){
		// Draw a sample using Van der Corput sequence.
		vec2 sampleVec = hammersleySample(uint(i), samplesCount);

		// Compute corresponding angles.
		float cosT2 = (1.0 - sampleVec.y)/(1.0+(alpha*alpha-1.0)*sampleVec.y);
		float cosT = sqrt(cosT2);
		float sinT = sqrt(1.0-cosT2);

		float angle = 2.0*M_PI*sampleVec.x;
		// Local half vector and light direction.
		vec3 h = normalize(sinT*cos(angle) * tangent + sinT*sin(angle) * binormal + cosT * n);
		vec3 l = normalize(-reflect(v,h));

		float NdotL = max(dot(n,l), 0.000);
		if(NdotL > 0.0){
			// Note that we don't have to flip the direction as we will write directly to the cubemap image.
			sum += NdotL * clamp(textureLod(samplerCube(texture0, sClampLinear), l, 0.0).rgb, 0.0, clampMax);
			denom += NdotL;
		}
	}
	return sum/denom;
}

/** Perform convolution of the cubemap with the BRDF lobe at a given roughness, using a high number of samples 
  and importance sampling. 
*/
void main(){
	ivec2 outSize = imageSize(outCube).xy;
	ivec3 iCoords = ivec3(gl_GlobalInvocationID.xyz);

	// Skip if outside.
	if(!all(lessThan(iCoords.xy, outSize))){
		return;
	}

	// Convert a texel coordinates to a direction.
	vec3 normCoords;
	normCoords.xy = 2.0 * vec2(iCoords.xy) / vec2(outSize) - 1.0;
	normCoords.z = 1.0;

	vec3 dir = vec3(0.0);
	if(iCoords.z == 0){
		dir = vec3( 1.0, -1.0, -1.0) * normCoords.zyx;

	} else if (iCoords.z == 1){
		dir = vec3(-1.0, -1.0,  1.0) * normCoords.zyx;

	} else if (iCoords.z == 2){
		dir = vec3( 1.0,  1.0,  1.0) * normCoords.xzy;

	} else if (iCoords.z == 3){
		dir = vec3( 1.0, -1.0, -1.0) * normCoords.xzy;

	} else if (iCoords.z == 4){
		dir = vec3( 1.0, -1.0,  1.0) * normCoords.xyz;

	} else {
		dir = vec3(-1.0, -1.0, -1.0) * normCoords.xyz;

	}

	// Evaluate the convolution and store the result.
	vec3 color = convo(normalize(dir), mipmapRoughness);
	imageStore(outCube, iCoords, vec4(color, 1.0));
}
